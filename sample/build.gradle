apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'androidx.navigation.safeargs'

group = 'com.emarsys.sample'

android {
    buildToolsVersion Config.buildToolsVersion
    compileSdkVersion Config.targetApiLevel
    defaultConfig {
        applicationId "com.emarsys.sample"
        minSdkVersion 21
        targetSdkVersion 30
        multiDexEnabled true
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "com.emarsys.sample.testutils.SampleAppTestRunner"
        resValue "string", "sdk_version", "${gitVersionName}"

        def clientId = null
        def localConfigPropertiesFile = rootProject.file('localConfig.properties')
        def localConfigProperties = new Properties()
        if (localConfigPropertiesFile.exists()) {
            localConfigProperties.load(new FileInputStream(localConfigPropertiesFile))
            clientId = localConfigProperties.getProperty("googleOAuthServerClientId")
        }

        buildConfigField "String", "GOOGLE_OAUTH_SERVER_CLIENT_ID", "\"${System.env.GOOGLE_OAUTH_SERVER_CLIENT_ID ?: clientId}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    if (project.devConfig.releaseSampleApp || System.env.DEVELOPMENT_MODE) {
        signingConfigs {
            release {
                storePassword System.env.RELEASE_STORE_PASSWORD ?: project.devConfig.releaseStorePassword
                keyAlias System.env.RELEASE_KEY_ALIAS ?: project.devConfig.releaseKeyAlias
                keyPassword System.env.RELEASE_KEY_PASSWORD ?: project.devConfig.releaseKeyPassword
                storeFile file(System.env.RELEASE_STORE_FILE ?: project.devConfig.releaseStoreFile)
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    buildTypes {
        debug {
            multiDexKeepProguard file("proguard-multidex-rules.pro")
            multiDexEnabled true
        }

    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.8'
    }
}

dependencies {
    if (project.devConfig.useLocalDependency || System.env.DEVELOPMENT_MODE) {
        implementation project(':emarsys-sdk')
    } else {
        implementation 'com.emarsys:emarsys-sdk:+'
    }
    // Getting a "Could not find firebase-core" error? Make sure you have
    // the latest Google Repository in the Android SDK manager

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.32"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.32"

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation 'io.coil-kt:coil:0.13.0'
    implementation 'com.chibatching.kotpref:kotpref:2.13.1'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation('com.schibsted.spain:barista:3.9.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'io.kotlintest:kotlintest-runner-junit4:3.1.11'
}

apply plugin: 'com.google.gms.google-services'